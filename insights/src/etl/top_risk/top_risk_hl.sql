DROP TABLE IF EXISTS ${hiveconf:__GREEN_MAIN_DB__}.TOP_RISK_HL_INPUT;

CREATE TABLE ${hiveconf:__GREEN_MAIN_DB__}.TOP_RISK_HL_INPUT(
    CLNT_OBJ_ID STRING,
    DB_SCHEMA STRING,
    MNGR_PERS_OBJ_ID STRING,
    PERS_OBJ_ID STRING,
    INDS_CLNT_CNT INT,
    INDS_EMPL_CNT INT,
    CLNT_EMPL_CNT INT,
    MNGR_TOT_RPT_CNT INT,
    MNGR_JOB_EMPL_CNT INT,
    JOB_CD STRING,
    SECTORCODE STRING,
    PRMRY_STD_JOB_TITL_KY STRING,
    FCTR_NM STRING,
    FCTR_VAL DOUBLE,
    MNGR_BNCHMRK_ORG_AVG DOUBLE,
    MNGR_BNCHMRK_ORG_STDDEV DOUBLE,
    CLNT_BNCHMRK_ORG_AVG DOUBLE,
    CLNT_BNCHMRK_ORG_STDDEV DOUBLE,
    INDS_BNCHMRK_ORG_AVG DOUBLE,
    INDS_BNCHMRK_ORG_STDDEV DOUBLE,
    TRNOVR_PRBLTY_PCT DOUBLE,
    TRNOVR_PRBLTY_RANGE_DSC STRING
)
USING PARQUET;

INSERT OVERWRITE TABLE ${hiveconf:__GREEN_MAIN_DB__}.TOP_RISK_HL_INPUT
SELECT
    CLNT_OBJ_ID,
    DB_SCHEMA,
    MNGR_PERS_OBJ_ID,
    PERS_OBJ_ID,
    INDS_CLNT_CNT,
    INDS_EMPL_CNT,
    CLNT_EMPL_CNT,
    MNGR_TOT_RPT_CNT,
    MNGR_JOB_EMPL_CNT,
    JOB_CD,
    SECTORCODE,
    PRMRY_STD_JOB_TITL_KY,
    FCTR_NM,
    FCTR_VAL,
    MNGR_BNCHMRK_ORG_AVG,
    MNGR_BNCHMRK_ORG_STDDEV,
    CLNT_BNCHMRK_ORG_AVG,
    CLNT_BNCHMRK_ORG_STDDEV,
    INDS_BNCHMRK_ORG_AVG,
    INDS_BNCHMRK_ORG_STDDEV,
    TRNOVR_PRBLTY_PCT,
    TRNOVR_PRBLTY_RANGE_DSC
FROM ${hiveconf:__GREEN_MAIN_DB__}.TOP_RISK_INSIGHTS
WHERE MNGR_TOT_RPT_CNT>3 AND MNGR_JOB_EMPL_CNT>2
AND (((FCTR_VAL/CLNT_BNCHMRK_ORG_AVG)*100 < 80) OR ((FCTR_VAL/INDS_BNCHMRK_ORG_AVG)*100 < 70));

DROP TABLE IF EXISTS ${hiveconf:__GREEN_MAIN_DB__}.TOP_RISK_HL;

CREATE TABLE ${hiveconf:__GREEN_MAIN_DB__}.TOP_RISK_HL(
    CLNT_OBJ_ID STRING,
    DB_SCHEMA STRING,
    MNGR_PERS_OBJ_ID STRING,
    MNGR_TOT_RPT_CNT INT,
    INDS_CLNT_CNT INT,
    INDS_EMPL_CNT INT,
    CLNT_EMPL_CNT INT,
    MNGR_JOB_EMPL_CNT INT,
    TOT_OUTLAYER_CNT INT,
    OUT_LAYER_PERC DOUBLE,
    JOB_CD STRING,
    PRMRY_STD_JOB_TITL_KY STRING,
    SECTORCODE STRING,
    FCTR_NM STRING,
    BNCHMRK_INFO STRING,
    INS_INFO STRING
)
USING PARQUET;

INSERT OVERWRITE TABLE ${hiveconf:__GREEN_MAIN_DB__}.TOP_RISK_HL
SELECT
    GRP.CLNT_OBJ_ID,
    GRP.DB_SCHEMA,
    GRP.MNGR_PERS_OBJ_ID,
    MNGR_TOT_RPT_CNT,
    INDS_CLNT_CNT,
    INDS_EMPL_CNT,
    CLNT_EMPL_CNT,
    MNGR_JOB_EMPL_CNT,
    TOT_OUTLAYER_CNT,
    OUT_LAYER_PERC,
    GRP.JOB_CD,
    GRP.PRMRY_STD_JOB_TITL_KY,
    GRP.SECTORCODE,
    GRP.FCTR_NM,
    BNCHMRK_INFO,
    INS_INFO
FROM
(SELECT
    CLNT_OBJ_ID,
    DB_SCHEMA,
    MNGR_PERS_OBJ_ID,
    JOB_CD,
    PRMRY_STD_JOB_TITL_KY,
    SECTORCODE,
    FCTR_NM,
    --COUNT(PERS_OBJ_ID) AS OUT_LAYER_COUNT,
    CONCAT_WS(',',COLLECT_LIST(CONCAT_WS(':',PERS_OBJ_ID,CONCAT("risk_type","=",TRNOVR_PRBLTY_RANGE_DSC),CONCAT("trnovr_prblty_pct","=",CAST(TRNOVR_PRBLTY_PCT AS STRING)),CONCAT("fctr_val","=",CAST(FCTR_VAL AS STRING))))) AS INS_INFO
    --CONCAT_WS(',',COLLECT_LIST(CONCAT_WS(':',PERS_OBJ_ID,CONCAT("risk_type","=",TRNOVR_PRBLTY_RANGE_DSC),CONCAT("trnovr_prblty_pct","=",CAST(TRNOVR_PRBLTY_PCT AS STRING)),CONCAT("fctr_val","=",CAST(FCTR_VAL AS STRING)),CONCAT("mngr_lvl_zscore","=",CAST(MNGR_LVL_ORG_ZSCORE AS STRING)),CONCAT("mngr_lvl_nor_zscore","=",CAST(MNGR_LVL_NOR_ZSCORE AS STRING)),CONCAT("clnt_lvl_zscore","=",CAST(CLNT_LVL_ORG_ZSCORE AS STRING)),CONCAT("clnt_lvl_nor_zscore","=",CAST(CLNT_LVL_NOR_ZSCORE AS STRING)),CONCAT("inds_lvl_zscore","=",CAST(INDS_LVL_ORG_ZSCORE AS STRING)),CONCAT("inds_lvl_nor_zscore","=",CAST(INDS_LVL_NOR_ZSCORE AS STRING))))) AS INS_INFO 
FROM
    ${hiveconf:__GREEN_MAIN_DB__}.TOP_RISK_HL_INPUT
    GROUP BY CLNT_OBJ_ID,DB_SCHEMA,MNGR_PERS_OBJ_ID,JOB_CD,PRMRY_STD_JOB_TITL_KY,SECTORCODE,FCTR_NM
)GRP
INNER JOIN
(SELECT
    DISTINCT
    INS.CLNT_OBJ_ID,
    INS.DB_SCHEMA,
    INS.MNGR_PERS_OBJ_ID,
    MNGR_TOT_RPT_CNT,
    JOB_CD,
    SECTORCODE,
    PRMRY_STD_JOB_TITL_KY,
    INDS_CLNT_CNT,
    INDS_EMPL_CNT,
    CLNT_EMPL_CNT,
    INS.FCTR_NM,
    MNGR_JOB_EMPL_CNT,
    TOT_OUTLAYER_CNT,
    ROUND((TOT_OUTLAYER_CNT/MNGR_TOT_RPT_CNT)*100, 2) AS OUT_LAYER_PERC,
    CONCAT(
       "{ \"mngr_avg\": ", COALESCE(CAST(MNGR_BNCHMRK_ORG_AVG AS STRING),'null'),
       ",\"mngr_stddev\": ", COALESCE(CAST(MNGR_BNCHMRK_ORG_STDDEV AS STRING),'null'),
       ",\"clnt_avg\": ", COALESCE(CAST(CLNT_BNCHMRK_ORG_AVG AS STRING),'null'),
       ",\"clnt_stddev\": ", COALESCE(CAST(CLNT_BNCHMRK_ORG_STDDEV AS STRING),'null'),
       ",\"inds_avg\": ", COALESCE(CAST(INDS_BNCHMRK_ORG_AVG AS STRING),'null'),
       ",\"inds_stddev\": ", COALESCE(CAST(INDS_BNCHMRK_ORG_STDDEV AS STRING),'null'),"}"
    ) AS BNCHMRK_INFO
FROM ${hiveconf:__GREEN_MAIN_DB__}.TOP_RISK_HL_INPUT INS
INNER JOIN
(SELECT CLNT_OBJ_ID,DB_SCHEMA,FCTR_NM,MNGR_PERS_OBJ_ID,COUNT(DISTINCT PERS_OBJ_ID) AS TOT_OUTLAYER_CNT FROM ${hiveconf:__GREEN_MAIN_DB__}.TOP_RISK_HL_INPUT
GROUP BY CLNT_OBJ_ID,DB_SCHEMA,FCTR_NM,MNGR_PERS_OBJ_ID) OUT_LAY
ON  INS.CLNT_OBJ_ID = OUT_LAY.CLNT_OBJ_ID
AND INS.DB_SCHEMA = OUT_LAY.DB_SCHEMA
AND INS.MNGR_PERS_OBJ_ID = OUT_LAY.MNGR_PERS_OBJ_ID
AND INS.FCTR_NM = OUT_LAY.FCTR_NM)INS
ON GRP.CLNT_OBJ_ID = INS.CLNT_OBJ_ID
AND GRP.DB_SCHEMA = INS.DB_SCHEMA
AND GRP.MNGR_PERS_OBJ_ID = INS.MNGR_PERS_OBJ_ID
AND GRP.JOB_CD = INS.JOB_CD
AND GRP.PRMRY_STD_JOB_TITL_KY = INS.PRMRY_STD_JOB_TITL_KY
AND GRP.SECTORCODE = INS.SECTORCODE
AND GRP.FCTR_NM = INS.FCTR_NM;

